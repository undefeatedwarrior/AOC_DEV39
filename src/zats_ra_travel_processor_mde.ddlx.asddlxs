@Metadata.layer:  #CUSTOMER
@UI.headerInfo: {
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: { value: 'TravelId' }, // Object page
    description: { value: '_Customer.FirstName'} // Object page

}
annotate entity ZATS_RA_TRAVEL_PROCESSOR with
{

  @UI.facet: [
  {
      purpose: #HEADER,
      type: #DATAPOINT_REFERENCE,
      label: 'Flight Ticket Price',
      position: 10,
      targetQualifier: 'price_data'
  },
  
  {
      purpose: #HEADER,
      type: #DATAPOINT_REFERENCE,
      label: 'Status',
      position: 20,
      targetQualifier: 'status'
  },
  
  {
      purpose: #STANDARD,
      type: #COLLECTION,
      position: 10,
      id: 'super',
      label: 'Additional Details'
  },
      {
          purpose: #STANDARD,
          type: #IDENTIFICATION_REFERENCE, //Only one is possible for a page
          label: 'More Info.',
          position: 10,
          parentId: 'super'
      },
      { 
          purpose: #STANDARD,
          type: #FIELDGROUP_REFERENCE, // Can be multiple for a page
          label: 'Price Data',
          position: 20,
          parentId: 'super',
          targetQualifier: 'pricing'
      },
      { 
          purpose: #STANDARD,
          type: #FIELDGROUP_REFERENCE,
          label: 'Dates',
          position: 30,
          parentId: 'super',
          targetQualifier: 'dates'
      },
//      { 
//          purpose: #STANDARD,
//          type: #FIELDGROUP_REFERENCE,
//          label: 'Admin Data',
//          position: 40,
//          parentId: 'super',
//          targetQualifier: 'admin'
//      }    

 
  {
      purpose: #STANDARD,
      type: #LINEITEM_REFERENCE,
      position: 20,
      label: 'Bookings',
      targetElement: '_Booking' // Sepatare Metadata Extension required 
  }
  ]


  @UI.selectionField: [{ position: 10 }]
  @UI.lineItem: [{ position: 10 }]
  @UI.identification: [{ position: 10 }]
  TravelId;
  
  @UI.selectionField: [{ position: 20 }]
  @UI.lineItem: [{ position: 20 }]
  @UI.identification: [{ position: 20 }]
  AgencyId;
  
  @UI.selectionField: [{ position: 30 }]
  @UI.lineItem: [{ position: 30 }]
  @UI.identification: [{ position: 30 }]
  CustomerId;
  
  @UI.lineItem: [{ position: 40 }]
  @UI.fieldGroup: [{ qualifier: 'dates', label: 'Start Date', position: 10 }]
  BeginDate;
  
  @UI.fieldGroup: [{ qualifier: 'dates', label: 'End Date', position: 20 }]
  EndDate;
  
  @UI.fieldGroup: [{ qualifier: 'pricing', label: 'Booking Fees', position: 10 }]
  BookingFee;
  
  @UI.selectionField: [{ position: 40 }]
  @UI.lineItem: [{ position: 50 }]
  @UI.dataPoint:{ qualifier: 'price_data', title: 'Flight Price'}
  @UI.fieldGroup: [{ qualifier: 'pricing', label: 'Total Price', position: 20 }]
  TotalPrice;
  
  @UI.fieldGroup: [{ qualifier: 'pricing', label: 'Currency Code', position: 30 }]
  CurrencyCode;
  
  //  Description;
  
  @UI.selectionField: [{ position: 50 }]
  @UI.lineItem: [{ position: 70 , criticality: 'Criticality', importance: #HIGH}]
  @UI.dataPoint:{ qualifier: 'status', title: 'Travel Status', criticality: 'Criticality'}
  @UI.fieldGroup: [{ qualifier: 'dates', label: 'Status', position: 30 }]
  OverallStatus;
  
  @UI.fieldGroup: [{ qualifier: 'admin', label: 'Created By', position: 10 }]
  CreatedBy;
  @UI.fieldGroup: [{ qualifier: 'admin', label: 'Created On', position: 20 }]
  CreatedAt;
  @UI.fieldGroup: [{ qualifier: 'admin', label: 'Changed By', position: 30 }]
  LastChangedBy;
  @UI.fieldGroup: [{ qualifier: 'admin', label: 'Changed On', position: 40 }]
  LastChangedAt;

}